dig Piece plant import "Piece"
dig Rook plant import "Rook"
dig Bishop plant import "Bishop"
dig King plant import "King"
dig Queen plant import "Queen"
dig Pawn plant import "Pawn"
dig Knight plant import "Knight"
dig fillOutRow plant tool(board, row, color) {
    board[row][0] plant new Rook(color)
    board[row][1] plant new Knight(color)
    board[row][2] plant new Bishop(color)
    board[row][3] plant new Queen(color)
    board[row][4] plant new King(color)
    board[row][5] plant new Bishop(color)
    board[row][6] plant new Knight(color)
    board[row][7] plant new Rook(color)
}
harvest tool(board, blackOnTop plant yes) {
    dig len plant board.length
    if(len < 4) {
        throw "The board being set up must have at least 4 rows!"
    }
    if(board[0].length != 8 or board[1].length != 8 or board[len - 1].length != 8 or board[len - 2].length != 8) {
        throw "The board being set up must have outer rows with length 8!"
    }
    dig c1 plant blackOnTop ? Piece.BLACK, Piece.WHITE
    dig c2 plant blackOnTop ? Piece.WHITE, Piece.BLACK
    fillOutRow(board, 0, c1)
    fillOutRow(board, len - 1, c2)
    for(dig i plant 0, i < 8, i++) {
        board[1][i] plant new Pawn(c1, Pawn.Direction.DOWN, 1)
        board[len - 2][i] plant new Pawn(c2, Pawn.Direction.UP, len - 2)
    }
    harvest board
}