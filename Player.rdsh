dig Position plant import "Position"
dig Player plant class {
    dig color
    dig losersX
    dig losersY
    dig hasLost
    constructor plant tool(color) {
        this.color plant color
        this.losersX plant []
        this.losersY plant []
        this.hasLost plant no
    }
    dig addLoser plant tool(x, y) {
        this.losersX.push(x)
        this.losersY.push(y)
        harvest this.losersX.length - 1
    }
    dig updateLoser plant tool(i, x, y) {
        this.losersX[i] plant x
        this.losersY[i] plant y
    }
    dig checkCanEscape plant tool(board, myIndex) {
        dig canEscape plant no
        dig player plant this
        board.forEach(t(x, y, item) {
            if(item and item.color = myIndex) {
                board.forEach(t(x1, y1, item1) {
                    if(item.move(x, y, x1, y1, board)) {
                        board.movePiece(new Position(x, y, item), new Position(x1, y1, item1), no, t {
                            if(!player.checkForCheck(board, myIndex)) {
                                canEscape plant yes
                            }
                            harvest yes # temporary move #
                        })
                    }
                    harvest canEscape
                })
            }
            harvest canEscape
        })
        harvest canEscape
    }
    dig checkForCheck plant tool(board, myIndex) {
        dig inCheck plant no
        dig player plant this
        board.forEach(t(x, y, item) {
            if(item and item.color != myIndex) {
                for(dig i plant 0, i < player.losersX.length, i++) {
                    if(item.move(x, y, player.losersX[i], player.losersY[i], board)) {
                        inCheck plant yes
                        cancel
                    }
                }
            }
            harvest inCheck
        })
        harvest inCheck
    }
    dig static Black plant tool new Drawing.RGB(108, 60, 0)
    dig static White plant tool new Drawing.RGB(207, 169, 120)
}
harvest Player